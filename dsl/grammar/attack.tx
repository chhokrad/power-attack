PowerAttackModel:
    'begin setup' 
        case_file=Case_file 
        setup_configs*=Setup_config[';'] 
    'end setup'
    ('begin precondition' 
        precondition*=Precondition_config[';']
    'end precondition')*
    ('begin attack scenarios'
        attack_scenarios+=Attack_scenario[';']
    'end attack scenarios')*
    
;
Deterministic_scalar:
    (INT | FLOAT | STRING)
;

Primitive_pair:
    (key=Deterministic_scalar ':' val=Deterministic_scalar)
;

Probablistic_scalar:
    '( random' distribution=Distribution_type ',' '{' params+=Primitive_pair[','] '}' ')'
;

Distribution_type:
    ('Uniform' | 'Uniform_discrete' | 'Gaussian' | 'Gaussian_discrete')
;

Scalar:
    (Deterministic_scalar | Probablistic_scalar)
;

Vector:
    '(' real=Scalar ',' imag=Scalar ')'
;

Value:
    Scalar | Vector
;

Pair:
    (key=Value ':' val=Value)
;

Time:
    '@ time' Value
;


Case_file:
    'Case :' case_file=STRING
;

Setup_config:
    'Setup ' (Protection_system_config | Controller_config | Tracer_config)
;

Protection_system_config:
    (Breaker_config | Relay_config)
;

Breaker_config:
    'Breaker @ ' id=Branch_type 'with' '{' params+=Pair[','] '}'
;

Relay_config:
    'Relay @ ' id=Branch_type 'of' type=Relay_type 'with' '{' params+=Pair[','] '}'
;

Relay_type:
    ('Over-current' | 'Distance' | 'Differential') 
;


Bus_type:
    'Bus ' id=Scalar
;

Branch_type:
    'Branch between' to_bus=Bus_type ',' from_bus=Bus_type
;

Controller_config:
    'Lfc ' '{' params+=Pair[','] '}'
;

Tracer_config:
    'Trace ' metric*=Metric_type[',']
;

Metric_type:
    'Bus Voltages' | 'Power Flows' | 'Islands' | 'Load-Loss' | 'Generator Frequency'
;

Precondition_config:
    (LoadSetting | TripSetting | FaultSetting)
;

LoadSetting:
    'Set load on ' id=Bus_type 'to' val=Value
;

TripSetting:
    'Trip' id=Node 
;

Node:
    (Bus_type | Branch_type)
;

FaultSetting:
    'Inject fault in ' id=Node  time=Time
;


Attack_scenario:
    label=ID ':' attack_sequence*=Attack['->']
;

Attack:
    element+=Attack_type[','] 
;

Attack_type:
    'Attack' (Generator_attack | Relay_attack | Breaker_attack)  time=Time
;

Generator_attack:
    'Lfc in Generator @ ' source=Bus_type 'with' type= Generator_attack_type 'attack' 'of' factor=Scalar
;

Generator_attack_type:
    ('Scaling' | 'Biasing')
;

Relay_attack:
    type=Relay_type 'Relay @ ' source=Branch_type   'with' kind=Relay_attack_type 
;

Relay_attack_type:
    ('Missed' | 'Spurious')
;

Breaker_attack:
    'Breaker @' source=Branch_type 'with' kind=Breaker_attack_type
;

Breaker_attack_type:
    ('Stuck_open' | 'Stuck_close')
;
