PowerAttackModel:
    'begin setup' 
        case_file=CaseFile 
        setup_configs*=Setup_config 
    'end setup'
    'begin precondition' 
        precondition*=Precondition_configs
    'end precondition'
    'begin attack scenarios'
        attack_scenarios+=Attack_scenario
    'end attack scenarios'
    
;

KeyValue_pair:
    '{' key=ID ':' value=FLOAT '}'
;

Composite_key_pair:
    '{' key=ID ':' value=Composite_type '}'
;

Composite_type:
    FLOAT | Random | 'unknown'
;

Time:
    'time' Composite_type
;

Random:
    '(' 'random' distribution=distribution_type ',' params+=KeyValue_pair ')'
;

COMPLEX:
    '(' real=Composite_type ',' imag=Composite_type ')'
;

distribution_type:
    'gaussian' | 'binomial' | 'poisson' | 'uniform'
;

CaseFile:
    'Case_file' case_file=STRING
;

Setup_config:
    ProtectSystemConfig | ControllerConfig | TracerConfig
;

ProtectSystemConfig:
    BreakerConfig | RelayConfig
;

BreakerConfig:
    'Breaker' id=Branch_type ',' tto=Composite_type ',' ttc=Composite_type
;

RelayConfig:
    'Relay' id=Branch_type ',' type=Relay_type ',' params+=Composite_key_pair
;

Relay_type:
    'Over-current' | 'Distance' | 'Differential' 
;


Bus_type:
    'Bus' id=Composite_type
;

Branch_type:
    to_bus=Bus_type ',' from_bus=Bus_type
;

ControllerConfig:
    'Agc' params+= Composite_type
;

TracerConfig:
    'Trace' metric+=Metric_type
;

Metric_type:
    'Bus Voltages' | 'Power Flows' | 'Islands' | 'Load-Loss'
;

Precondition_configs:
    LoadSetting | TripSetting | FaultSetting
;

LoadSetting:
    'Load' id=Bus_type ',' value=Composite_type
;

TripSetting:
    'Trip' id=Bus_type ',' time=Time
;


FaultSetting:
    'Fault' id=Bus_type ',' time=Time
;


Attack_scenario:
    attack_sequence*=Attack
;

Attack:
    'Attack' label=ID '->' element+=Attack_type 
;

Attack_type:
    (GeneratorAttack | RelayAttack | BreakerAttack)+ ',' time=Time
;

GeneratorAttack:
    'Generator' source=Bus_type ',' type= Generator_attack_type ',' factor=Composite_type
;

Generator_attack_type:
    ('Scaling' | 'Biasing')
;

RelayAttack:
    'Relay' source=Branch_type ',' type=Relay_attack_type 
;

Relay_attack_type:
    ('Missed' | 'Spurious')
;

BreakerAttack:
    'Breaker' source=Branch_type ',' type=Breaker_attack_type
;

Breaker_attack_type:
    ('Stuck_open' | 'Stuck_close')
;
